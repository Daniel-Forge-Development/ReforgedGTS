plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

forgeVersion = "1.20.1"

minecraft {
    mappings channel: 'official', version: '1.20.1'
    copyIdeResources = true
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven { url 'https://jitpack.io' }

    ivy {
        setUrl('https://download.nodecdn.net/containers/reforged/server/release')
        metadataSources {
            artifact()
        }
        patternLayout {
            artifact('[revision]/[artifact].[ext]')
        }
    }

    maven {
        url "https://maven.envyware.co.uk/releases"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.1.44"

    implementation 'pixelmon:Pixelmon-1.20.1-9.2.0-server:9.2.0'

    compileOnly 'com.github.EnvyWare.ForgePlaceholderAPI:api:0.0.1'

    shadow group: 'org.spongepowered', name: 'configurate-yaml', version: '4.0.0'

    shadow group: 'com.envyful.api', name: 'commons', version: '5.0.1'
    shadow (group: 'com.envyful.api', name: 'forge20', version: '5.0.1') {
        transitive = false;
    }
    shadow (group: 'com.envyful.api', name: 'reforged20', version: '5.0.1') {
        transitive = false;
    }

    shadow project(':api')
}

shadowJar {
    finalizedBy('reobfJar')
    zip64 = true
    configurations = [project.configurations.shadow]
    setArchiveClassifier('')
    setArchiveBaseName(rootProject.name + "-Forge")

    relocate('org.spongepowered.configurate', 'com.envyful.gts.forge.shade.configurate')
    relocate('org.yaml.snakeyaml', 'com.envyful.gts.forge.shade.snakeyaml')
    relocate('io.leangen.geantyref', 'com.envyful.gts.forge.shade.geantyref')
    relocate('com.google.gson', 'com.envyful.gts.forge.shade.gson')
    relocate('com.zaxxer', 'com.envyful.gts.forge.shade.hikari')
    relocate('org.slf4j', 'com.envyful.gts.forge.shade.slf4j')
    relocate('com.envyful.api', 'com.envyful.gts.forge.shade.envy.api')

    exclude "**/module-info.class"
}

jar.finalizedBy('shadowJar')
build.finalizedBy('versionedRelease')